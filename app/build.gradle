apply plugin: 'com.android.application'


repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        multiDexEnabled true
        applicationId "org.openlmis.core"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        flavorDimensions "1"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        if (project.hasProperty("prodsim")) {
            versionCode((new Date().getTime() / 1000).toInteger())
            versionName "$prodsim"
        } else {
            versionCode androidVersionCode.toInteger()
            versionName semanticVersion
        }
        buildConfigField "boolean", "MONITOR_DEVICE_ID", "true"
    }

    signingConfigs {
        debug {
            storeFile file("../scripts/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../scripts/lmis_moz.jks")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "clintonhealthaccess"
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        local {
            applicationId 'org.openlmis.core.local'
        }
        dev {
            applicationId 'org.openlmis.core.dev'
        }
        qa {
            applicationId 'org.openlmis.core.qa'
            buildConfigField "boolean", "MONITOR_DEVICE_ID", "false"
        }
        uat {
            applicationId 'org.openlmis.core.uat'
        }
        training {
            applicationId 'org.openlmis.core.training'
        }
        prd {
            applicationId 'org.openlmis.core'
        }
        ci {
            applicationId 'org.openlmis.core.ci'
        }
        showcase {
            applicationId 'org.openlmis.core.showcase'
        }
    }

    //the following code will change release apk file names to: {applicationId}-{versionCode}-release.apk
    //this is needed for in-app upgrade to work, because it's how the upgrade server matches package names
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName.endsWith('.apk') && outputFileName.contains("-release")) {
                outputFileName = "${applicationId}-${versionCode}-release.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}



apply plugin: 'checkstyle'
apply plugin: 'findbugs'


task checkstyle(type: Checkstyle) {
    configFile file("config/quality/checkstyle/checkstyle.xml")
    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/migrations/**'

    classpath = files()
}

task checkci(dependsOn: ['checkstyle', 'testLocalDebugUnitTest'])

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("config/quality/findbugs/findbugs-filter.xml")
    classes = files("$project.buildDir/intermediates/classes/")

    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        html.enabled = true
        xml.enabled = false
    }

    classpath = files()
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor "org.projectlombok:lombok:1.12.6"
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp:1.5.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.microsoft.appcenter:appcenter-analytics:${rootProject.ext.appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${rootProject.ext.appCenterSdkVersion}"
    implementation 'com.android.support:support-annotations:22.2.1',
            'com.android.support:appcompat-v7:22.2.1',
            'com.android.support:design:22.2.1',
            'com.android.support:support-v4:27.1.0',
            //https://issuetracker.google.com/issues/37070144
            'com.android.support:recyclerview-v7:26.1.0',
            'com.android.support:cardview-v7:22.2.1',
            'com.google.android.gms:play-services-analytics:16.0.8',
            'com.github.viethoa:fastscroller:1.2.0',
            'io.reactivex:rxjava:1.0.14',
            'io.reactivex:rxandroid:1.0.1',
            'com.squareup.retrofit:retrofit:1.9.0',
            'com.squareup.okhttp:okhttp-urlconnection:2.2.0',
            'com.squareup.okhttp:okhttp:2.2.0',
            'com.j256.ormlite:ormlite-android:4.45',
            'org.apmem.tools:layouts:1.10@aar',
            'org.roboguice:roboguice:3.0.1',
            'org.apache.commons:commons-lang3:3.3.2',
            'net.danlew:android.joda:2.9.1'
    implementation 'org.projectlombok:lombok:1.12.6'
    testImplementation 'junit:junit:4.12',
            'org.hamcrest:hamcrest-all:1.3',
            'org.mockito:mockito-all:1.10.19',
            'org.robolectric:robolectric:3.8',
            'org.robolectric:shadows-multidex:3.8',
            'org.robolectric:shadows-support-v4:3.3.2',
            'org.assertj:assertj-core:1.7.1',
            'com.natpryce:make-it-easy:4.0.1'

    implementation('com.google.android.gms:play-services-analytics:7.8.0') {
        exclude module: "com.android.support:support-v4:23.2.1"
    }
    implementation('org.roboguice:roboblender:3.0.1'){
        exclude module: "com.android.support:support-v4:23.2.1"
    }
    implementation('org.roboguice:roboguice:3.0.1'){
        exclude module: "com.android.support:support-v4:23.2.1"
    }
    implementation('com.android.support:design:22.2.1') {
        exclude module: "com.android.support:support-v4:23.2.1"
    }
    implementation('com.android.support:appcompat-v7:22.2.1') {
        exclude module: "com.android.support:support-v4:23.2.1"
    }

}

configurations.all {
    resolutionStrategy.force 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    resolutionStrategy.force 'com.squareup.okhttp:okhttp:2.2.0'
    resolutionStrategy.force 'com.android.support:support-core-ui:26.1.0'
}

task bundleInstall(type: Exec) {
    workingDir "$projectDir/../functionalTests"
    executable 'bundle'
    args 'install'
}

task functionalTests(type: Exec, dependsOn: ['bundleInstall', 'assembleLocalDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-local-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @dev --format html --out reports.html"
}

task upgradeTestSetup(type: Exec, dependsOn: ['bundleInstall']) {
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    executable 'calabash-android'
    args "run", "$projectDir/../functionalTests/oldVersionApk/app-upgrade-v1.11.82-debug.apk", "--tags @upgrade_setup --format html --out upgrade_test_setup_reports.html"
}

task upgradeTests(type: Exec, dependsOn: ['bundleInstall', 'upgradeTestSetup', 'assembleUpgradeDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-upgrade-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @upgrade --format html --out upgrade_test_reports.html"
}

task regressionTests(type: Exec, dependsOn: ['bundleInstall', 'assembleRegressionDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-regression-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @regression --format html --out regression_test_reports.html"
}

task weeklyTests(type: Exec, dependsOn: ['bundleInstall', 'assembleRegressionDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-regression-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @weekly --format html --out weekly_test_reports.html"
}

task stressTests(type: Exec, dependsOn: ['bundleInstall', 'assembleRegressionDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-regression-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @STRESS_TEST --format html --out stress_test_report.html"
}

task monkeyTests(type: Exec, dependsOn: ['bundleInstall', 'assembleProdsimDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-prodsim-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @monkey --format html --out monkey_test_report.html"
}

apply from: "${rootProject.projectDir}/gradle/jacoco.gradle"
apply from: "${rootProject.projectDir}/gradle/appcenter.gradle"
